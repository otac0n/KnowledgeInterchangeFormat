@namespace KnowledgeInterchangeFormat
@classname KifParser
@using System.Globalization
@using System.Linq
@using KnowledgeInterchangeFormat.Expressions
@using KnowledgeInterchangeFormat.Properties
@start knowledgeBase

character = [\0-\x7F]
int <int> = d:("" digit+) { int.Parse(d) }

upper = [A-Z]
lower = [a-z]
digit = [0-9]
alpha = [-!$%&*+./:<=>?@_~]
white = [ \t\r\n\f]
normal = upper / lower / digit / alpha

word <Tuple<string, string>> -memoize
  = &normal w:(<Tuple<string, string>>
    c:normal           { Tuple.Create(c.ToUpperInvariant(), c) }
    / "\\" c:character { Tuple.Create(c, c) }
  )+ { Tuple.Create(
    string.Concat(w.Select(c => c.Item1)),
    string.Concat(w.Select(c => c.Item2)))
  }

charref <CharacterReference> = "#\\" c:character { new CharacterReference(c[0]) }

string <CharacterString> = '"' s:quotable '"' { new CharacterString(s) }
quotable -memoize = s:(strchar / "\\" c:character { c }) { string.Concat(s) }
strchar = !["\\] character

block <CharacterBlock> = "#" n:int "q"i v:#parse{{
    if (state.Location + n <= state.Subject.Length)
    {
        var result = state.Subject.Substring(state.Location, n);
        var startCursor = state;
        var endCursor = state = state.Advance(n);
        return new ParseResult<string>(startCursor, endCursor, result);
    }

    return null;
}} { new CharacterBlock(v) }

variable <Variable> = indvar / seqvar
indvar <IndividualVariable> = n:word &{ n.Item1.StartsWith("?") } { new IndividualVariable(n.Item1, n.Item2) }
seqvar <SequenceVariable> = n:word &{ n.Item1.StartsWith("@") } { new SequenceVariable(n.Item1, n.Item2) }

operator <Operator> -memoize = w:word &{ this.operators.Contains(w.Item1) } { new Operator(w.Item1, w.Item2) }
constant <Constant> = ![?@] w:word !{ this.operators.Contains(w.Item1) } { new Constant(w.Item1, w.Item2) }

term <Term> = indvar / constant / charref / string / block / funterm / listterm / quoterm / logterm

funterm <FunctionalTerm>
  = lParen _ c:constant _ a:term<0,,_> _ s:seqvar? _ rParen { new ImplicitFunctionalTerm(c, a, s.SingleOrDefault()) }
  / lParen _ o:operator &{ o.Id == "VALUE" } _ f:term _ a:term<0,,_> _ s:seqvar? _ rParen { new ExplicitFunctionalTerm(f, a, s.SingleOrDefault()) }

listterm <ListTerm>
  = lParen _ o:operator &{ o.Id == "LISTOF" } _ a:term<0,,_> _ s:seqvar? _ rParen { new ListTerm(a, s.SingleOrDefault()) }

quoterm <Quotation>
  = lParen _ o:operator &{ o.Id == "QUOTE" } _ l:listexpr _ rParen { new Quotation(l) }
  / "'" _ l:listexpr { new Quotation(l) }
listexpr <Expression> = atom / lParen _ l:listexpr<0,,_> _ rParen { new ListExpression(l) }
atom <Expression>
  = (variable / operator / constant)
  / charref
  / string
  / block

logterm <LogicalTerm>
  = lParen _ o:operator &{ o.Id == "IF" } _ c:logpair<1,,_> _ d:term? _ rParen { new IfTerm(c, d.SingleOrDefault()) }
  / lParen _ o:operator &{ o.Id == "COND" } _ c:logitem<0,,_> _ rParen { new ConditionalTerm(c) }
logpair <LogicalPair> = c:sentence _ v:term { new LogicalPair(c, v) }
logitem <LogicalPair> = lParen _ p:logpair _ rParen { p }

sentence <Sentence> = c:constant { new ConstantSentence(c) } / equation / inequality / relsent / logsent / quantsent

equation <Equation> = lParen _ o:operator &{ o.Id == "=" } _ l:term _ r:term _ rParen { new Equation(l, r) }
inequality <Inequality> = lParen _ o:operator &{ o.Id == "/=" } _ l:term _ r:term _ rParen { new Inequality(l, r) }

relsent <RelationalSentence>
  = lParen _ c:constant _ t:term<0,,_> _ s:seqvar? _ rParen { new ImplicitRelationalSentence(c, t, s.SingleOrDefault()) }
  / lParen _ o:operator &{ o.Id == "HOLDS" } _ r:term _ a:term<0,,_> _ s:seqvar? _ rParen { new ExplicitRelationalSentence(r, a, s.SingleOrDefault()) }

logsent <LogicalSentence>
  = lParen _ o:operator &{ o.Id == "NOT" } _ s:sentence       _ rParen { new Negation(s) }
  / lParen _ o:operator &{ o.Id == "AND" } _ c:sentence<0,,_> _ rParen { new Conjunction(c) }
  / lParen _ o:operator &{ o.Id == "OR"  } _ d:sentence<0,,_> _ rParen { new Disjunction(d) }
  / lParen _ o:operator &{ o.Id == "=>"  } _ a:sentence<0,,_> _ c:sentence _ rParen { new Implication(a, c) }
  / lParen _ o:operator &{ o.Id == "<="  } _ c:sentence _ a:sentence<0,,_> _ rParen { new ReverseImplication(c, a) }
  / lParen _ o:operator &{ o.Id == "<=>" } _ l:sentence _ r:sentence _ rParen { new Equivalence(l, r) }

quantsent <QuantifiedSentence>
  = lParen _ o:operator &{ o.Id == "FORALL" } _ lParen _ v:varspec<1,,_> _ rParen _ s:sentence _ rParen { new UniversallyQuantifiedSentence(v, s) }
  / lParen _ o:operator &{ o.Id == "EXISTS" } _ lParen _ v:varspec<1,,_> _ rParen _ s:sentence _ rParen { new ExistentiallyQuantifiedSentence(v, s) }

varspec <VariableSpecification>
  = v:variable { new VariableSpecification(v, null) }
  / lParen _ v:variable _ c:constant _ rParen { new VariableSpecification(v, null) }

definition <Definition> = unrestricted / complete / partial

unrestricted <UnrestrictedDefinition>
  = lParen _ o:operator &{ o.Id == "DEFOBJECT"   } _ c:constant _ d:string? _ s:sentence<0,,_> _ rParen { new UnrestrictedObjectDefinition(c, d.SingleOrDefault(), s) }
  / lParen _ o:operator &{ o.Id == "DEFFUNCTION" } _ c:constant _ d:string? _ s:sentence<0,,_> _ rParen { new UnrestrictedFunctionDefinition(c, d.SingleOrDefault(), s) }
  / lParen _ o:operator &{ o.Id == "DEFRELATION" } _ c:constant _ d:string? _ s:sentence<0,,_> _ rParen { new UnrestrictedRelationDefinition(c, d.SingleOrDefault(), s) }
  / lParen _ o:operator &{ o.Id == "DEFLOGICAL"  } _ c:constant _ d:string? _ s:sentence<0,,_> _ rParen { new UnrestrictedLogicalDefinition(c, d.SingleOrDefault(), s) }

complete <CompleteDefinition>
  = lParen _ o:operator &{ o.Id == "DEFOBJECT"   } _ c:constant _                                                d:string? _ ":=" _ t:term     _ rParen { new CompleteObjectDefinition(c, d.SingleOrDefault(), t) }
  / lParen _ o:operator &{ o.Id == "DEFFUNCTION" } _ c:constant _ lParen _ a:indvar<0,,_> _ v:seqvar? _ rParen _ d:string? _ ":=" _ t:term     _ rParen { new CompleteFunctionDefinition(c, d.SingleOrDefault(), a, v.SingleOrDefault(), t) }
  / lParen _ o:operator &{ o.Id == "DEFRELATION" } _ c:constant _ lParen _ a:indvar<0,,_> _ v:seqvar? _ rParen _ d:string? _ ":=" _ s:sentence _ rParen { new CompleteRelationDefinition(c, d.SingleOrDefault(), a, v.SingleOrDefault(), s) }
  / lParen _ o:operator &{ o.Id == "DEFLOGICAL"  } _ c:constant _                                                d:string? _ ":=" _ s:sentence _ rParen { new CompleteLogicalDefinition(c, d.SingleOrDefault(), s) }

partial <PartialDefinition>
  = lParen _ o:operator &{ o.Id == "DEFOBJECT"   } _ c:constant _                                                d:string? _ ":->" _ x:indvar _ ":=>" _ s:sentence _ rParen { new PartialObjectDefinition(c, d.SingleOrDefault(), x, s) }
  / lParen _ o:operator &{ o.Id == "DEFOBJECT"   } _ c:constant _                                                d:string? _ ":->" _ x:indvar _ ":<=" _ s:sentence _ rParen { new ReversePartialObjectDefinition(c, d.SingleOrDefault(), x, s) }
  / lParen _ o:operator &{ o.Id == "DEFFUNCTION" } _ c:constant _ lParen _ a:indvar<0,,_> _ v:seqvar? _ rParen _ d:string? _ ":->" _ x:indvar _ ":=>" _ s:sentence _ rParen { new PartialFunctionDefinition(c, d.SingleOrDefault(), a, v.SingleOrDefault(), x, s) }
  / lParen _ o:operator &{ o.Id == "DEFFUNCTION" } _ c:constant _ lParen _ a:indvar<0,,_> _ v:seqvar? _ rParen _ d:string? _ ":->" _ x:indvar _ ":<=" _ s:sentence _ rParen { new ReversePartialFunctionDefinition(c, d.SingleOrDefault(), a, v.SingleOrDefault(), x, s) }
  / lParen _ o:operator &{ o.Id == "DEFRELATION" } _ c:constant _ lParen _ a:indvar<0,,_> _ v:seqvar? _ rParen _ d:string? _                    ":=>" _ s:sentence _ rParen { new PartialRelationDefinition(c, d.SingleOrDefault(), a, v.SingleOrDefault(), s) }
  / lParen _ o:operator &{ o.Id == "DEFRELATION" } _ c:constant _ lParen _ a:indvar<0,,_> _ v:seqvar? _ rParen _ d:string? _                    ":<=" _ s:sentence _ rParen { new ReversePartialRelationDefinition(c, d.SingleOrDefault(), a, v.SingleOrDefault(), s) }
  / lParen _ o:operator &{ o.Id == "DEFLOGICAL"  } _ c:constant _                                                d:string? _                    ":=>" _ s:sentence _ rParen { new PartialLogicalDefinition(c, d.SingleOrDefault(), s) }
  / lParen _ o:operator &{ o.Id == "DEFLOGICAL"  } _ c:constant _                                                d:string? _                    ":<=" _ s:sentence _ rParen { new ReversePartialLogicalDefinition(c, d.SingleOrDefault(), s) }

form <Form> = sentence / definition

knowledgeBase <KnowledgeBase> = _ f:form<0,,_> _ EOF { new KnowledgeBase(f) }

_ -memoize = "" (white / EOL / comment)*

comment
  = ";" (!eolChar .)* EOL?

lParen = "(";
rParen = ")" / #error{ "KIF001:" + Resources.KIF001_ERROR_PARSER_ExpectedParenthesis }

EOL
  = "\n"
  / "\r\n"
  / "\r"

eolChar
  = [\n\r]

EOF
  = !.
  / unexpected:&. #error{ "KIF002:" + string.Format(CultureInfo.CurrentCulture, Resources.KIF002_ERROR_PARSER_UnexpectedEndOfInput, unexpected) }
